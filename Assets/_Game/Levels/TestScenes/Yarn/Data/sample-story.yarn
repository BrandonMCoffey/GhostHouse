title: Kitchen
position: -480,-307
---
// for organizing the room
<<if false>>
    <<jump book_on_table>>
    <<jump open_cage>>
<<endif>>
===

title: book_on_table
position: -482,-181
---
// be careful of empty lines when within an option.
// the below snippet would not consider "No" as an option, so yes would be auto-selected
//      Would you like to push over this book?
//      -> Yes
//              <<expend_sp 2>>
//      
//              <<set $loc_book = "floor">>
//      -> No

Would you like to push over this book?
-> Yes
        <<expend_sp 2>>                         // take 2 spirit points from the player
        <<animate I_book table_to_floor wait>>  // play the "table_to_floor" timeline on the I_book gameobject. Dialog will wait for this animation to finish before continuing
        <<set $loc_book = "floor">>
-> No

// requisite to
<<if false>>                                    // could use this little hackery if you want the arrows in the graph preview
    <<jump book_on_floor>>
<<endif>>
===

title: book_on_floor
position: -482,-93
---
Would you like to read this book?
-> Yes
    <<animate I_book read wait>>
    <<set $loc_key = "floor">>
    <<add_journal_entry random_fact>>       // add entry "random_fact" to the player's journal
-> No

// requisite to
<<if false>>
    <<jump key_from_book>>
<<endif>>
===

title: key_from_book
position: -482,-15
---
[interaction/][item_sprite="key"/][spirit_points=3/]Would you like to pick up this key?
-> Yes
    <<locked_animate I_key pickup>>
    <<set $loc_key = "player">>
-> No
-> [cancel/]

// requisite to
<<if false>>
    <<jump door_sisters>>
<<endif>>
===

title: open_cage
position: -707,-180
---
Would you like to open the bird cage?
-> Yes
    <<animate I_birdCage open>>
    <<set $loc_bird = "living">>
    <<jump parents_argue>>
-> No
    <<animate I_birdCage playWith>>
===

title: parents_argue
position: -707,-93
---
// override default npc portrait for the "gasp" sprite
<<emote gasp>>
man: No way! Goldfish are way better than Cheezits!

<<emote angry>>
woman: stop this non-sense. You are longer allowed in this house.

// example for markup, not sure what types of markup will be implemented
// yarn spinner also has a few markup attributes implemented,
// which can be seen here: https://docs.yarnspinner.dev/getting-started/writing-in-yarn/markup#text-replacement-markers
ghost: Super [wavy]spooky[/wavy]!
===


title: Sisters_Room
position: -74,-257
---
// for organizing the room
<<if false>>
    <<jump door_sisters>>
<<endif>>
===

title: door_sisters
position: -75,-13
---
<<if $loc_key == "player">>                     // if player has the key
    Would you like to unlock the door?
    -> Yes
        <<animate I_door open wait>>
        <<set $sisters_room_unlocked = true>>
    -> No
<<else>>                                        // else key is elsewhere
    <<animate I_door locked>>
    The door seems to be locked.
<<endif>>

// requisite to
<<if false>>
    <<jump bear_pickup>>
<<endif>>
===

title: bear_pickup
position: -75,65
---
Would you like to take this Teddy Bear?
-> Yes
        <<animate I_teddyBear pickup>>
        <<set $loc_teddyBear = "player">>       // narrative can be split into more
        <<jump bear_move>>                      // nodes for stronger readibility
-> No
===

title: bear_move
position: -75,140
---
What would you like to do with this Teddy Bear?
-> Move to Closet
    <<animate I_teddyBear place_in_closet>>
    <<set $loc_teddyBear = "closet">>
-> Hold on to it for now.
===